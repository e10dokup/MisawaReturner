// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.6'
    }
}

subprojects {
    repositories {
        jcenter()
        maven {
            url "${System.env.HOME}/.m2/repository"
        }
    }

    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

// http://www.gradle.org/docs/current/dsl/org.gradle.api.plugins.ExtraPropertiesExtension.html
project.ext {
    compileSdkVersion = 23
    buildToolsVersion = '23.0.0'

    // see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
    // CIサーバなどでは -PdisablePreDex をつける。 preDexが省略されてビルドが速くなる
    preDexLibs = !project.hasProperty('disablePreDex')

    supportPackageVersion = '23.0.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}